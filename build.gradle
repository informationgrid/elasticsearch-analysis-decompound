buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://xbib.org/repository'
        }
    }
    dependencies {
        classpath "org.xbib.elasticsearch:gradle-plugin-elasticsearch-build:6.3.2.1"
    }
}

import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
    id "org.sonarqube" version "2.6.1"
    id "io.codearte.nexus-staging" version "0.11.0"
    id "org.xbib.gradle.plugin.asciidoctor" version "1.5.6.0.1"
}

printf "Date: %s\nHost: %s\nOS: %s %s %s\nJVM: %s %s %s %s\nGradle: %s Groovy: %s Java: %s\n" +
        "Build: group: ${project.group} name: ${project.name} version: ${project.version}\n",
        ZonedDateTime.now().format(DateTimeFormatter.ISO_DATE_TIME),
        InetAddress.getLocalHost(),
        System.getProperty("os.name"),
        System.getProperty("os.arch"),
        System.getProperty("os.version"),
        System.getProperty("java.version"),
        System.getProperty("java.vm.version"),
        System.getProperty("java.vm.vendor"),
        System.getProperty("java.vm.name"),
        gradle.gradleVersion, GroovySystem.getVersion(), JavaVersion.current()

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'org.xbib.gradle.plugin.elasticsearch.plugin'
apply plugin: 'org.xbib.gradle.plugin.asciidoctor'

esplugin {
    description 'Decompound plugin for Elasticsearch'
    classname 'org.xbib.elasticsearch.plugin.analysis.decompound.AnalysisDecompoundPlugin'
    name 'elasticsearch-analysis-decompound'
    url 'https://github.com/jprante/elasticsearch-analysis-decompound'
}

configurations {
    wagon
}

dependencies {
    compile "org.elasticsearch:elasticsearch:${project.property('elasticsearch.version')}"
    testCompile "org.apache.logging.log4j:log4j-core:${project.property('log4j.version')}"
    testCompile "org.xbib.elasticsearch:elasticsearch-analysis-common:${project.property('xbib-elasticsearch-test.version')}"
    testCompile "org.elasticsearch.plugin:transport-netty4-client:${project.property('elasticsearch.version')}"
    wagon "org.apache.maven.wagon:wagon-ssh:${project.property('wagon.version')}"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

esTest {
    dependsOn jar
    enabled = true
}

test {
    enabled = false
}

randomizedTest {
    enabled = false
}

clean {
    delete fileTree('.') { include '.local*.log' }
}

artifacts {
    archives sourcesJar, javadocJar
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

apply from: 'gradle/ext.gradle'
apply from: 'gradle/publish.gradle'
apply from: 'gradle/sourcequality.gradle'

asciidoctor {
    attributes toc: 'left',
            doctype: 'book',
            icons: 'font',
            encoding: 'utf-8',
            sectlink: true,
            sectanchors: true,
            linkattrs: true,
            imagesdir: 'img',
            'source-highlighter': 'coderay'
}

// no dependency licenses
dependencyLicenses.enabled = false
// no integTest yet
integTest.enabled = false

task fixVersion << {
    ant.replaceregexp(match:'elasticsearch.version=6.3.2', replace:'elasticsearch.version=6.8.4', flags:'g', byline:true) {
        fileset(dir: 'build/generated-resources', includes: 'plugin-descriptor.properties')
    }
    ant.zip(destfile: "build/distributions/elasticsearch-analysis-decompound-6.8.4.0.zip") {
        fileset(dir: "build/generated-resources")
        fileset(dir: "build/distributions") {
            include(name: "elasticsearch-analysis-decompound-6.8.4.0.jar")
        }
    }
}

//tasks.uploadArchives.dependsOn 'myCopy'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://nexus.informationgrid.eu/repository/maven-releases/") {
                authentication(userName: "$NEXUS_USERNAME", password: "$NEXUS_PASSWORD")
            }
        }
    }
}
